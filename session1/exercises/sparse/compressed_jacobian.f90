! Module containing the nbdirsmax variable required by central_diff_dv
module diffsizes
  implicit none
  integer, parameter :: nbdirsmax = 3
end module diffsizes

! Include the vector forward mode derivative generated by Tapenade
include "central_diff_dv.f90"

! Program for computing a tridiagonal Jacobian using compression
program compressed_jacobian
  use diffsizes, only: m => nbdirsmax  ! Number of seed vectors

  implicit none

  integer, parameter :: n = 10         ! Number of grid points
  real, parameter :: h = 1.0           ! Uniform grid spacing
  real, dimension(n) :: u              ! Input vector
  real, dimension(n) :: approx         ! Central difference approximation
  real, dimension(m,n) :: seed         ! Seed matrix for the VJP
  real, dimension(m,n) :: compressed   ! Compressed Jacobian for the central difference calculation
  real, dimension(n,n) :: jacobian     ! Jacobian for the central difference calculation
  integer :: i, j                      ! Dummy indices for looping

  ! Specify some arbitrary input
  u(:) = 1.0

  ! Set up the seed matrix as a 3xn array
  seed(:,:) = 0.0
  do i = 1, m
    seed(i,i::m) = 1.0
  end do

  ! Apply the VJP
  call central_diff_dv(u, seed, approx, compressed, h, n, m)

  ! Write out the compressed result to file
  open(unit=10, file="compressed_jacobian.dat")
  do i = 1, m
    write(unit=10, fmt=100) compressed(i,:)
  end do
  100 format(10(f4.1,","))
  close(unit=10)

  ! Decompress rows and insert them into the Jacobian
  do i = 1, m
    j = i
    jacobian(i::m,:) = 0.0
    do while (j <= n)
      if (j == 1) then
        jacobian(j,n) = compressed(i,n)
      else
        jacobian(j,j-1) = compressed(i,j-1)
      end if
      jacobian(j,j) = compressed(i,j)
      if (j == n) then
        jacobian(j,1) = compressed(i,1)
      else
        jacobian(j,j+1) = compressed(i,j+1)
      end if
      j = j + m
    end do
  end do

  ! Write out the result to file
  open(unit=11, file="decompressed_jacobian.dat")
  do i = 1, n
    write(unit=11, fmt=100) jacobian(i,:)
  end do
  close(unit=11)

end program compressed_jacobian
