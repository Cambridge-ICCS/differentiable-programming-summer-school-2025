! Module containing the nbdirsmax variable required by central_diff_dv
module diffsizes
  implicit none
  integer, parameter :: nbdirsmax = 10
end module diffsizes

! Include the vector forward mode derivative generated by Tapenade
include "central_diff_dv.f90"

! Naive program for computing a tridiagonal Jacobian
program dense_jacobian
  use diffsizes, only: m => nbdirsmax  ! Number of seed vectors

  implicit none

  logical, parameter :: output = .true. ! Flag for whether to write output to file
  integer, parameter :: n = 10          ! Number of grid points
  real, parameter :: h = 1.0            ! Uniform grid spacing
  real, dimension(n) :: u               ! Input vector
  real, dimension(n) :: approx          ! Central difference approximation
  real, dimension(m,n) :: seed          ! Seed matrix for the VJP
  real, dimension(m,n) :: jacobian      ! Jacobian for the central difference calculation
  integer :: i                          ! Dummy index for looping

  if (m /= n) then
    print *, "Error: number of grid points must match number of seed vectors."
    stop 1
  end if

  ! Specify some arbitrary input
  u(:) = 1.0

  ! Set up the seed matrix as the nxn identity
  seed(:,:) = 0.0
  do i = 1, n
    seed(i,i) = 1.0
  end do

  ! Propagate the seed matrix through the VJP
  call central_diff_dv(u, seed, approx, jacobian, h, n, n)

  ! Write out the result to file
  if (output) then
    open(unit=10, file="dense_jacobian.dat")
    do i = 1, n
      write(unit=10, fmt=100) jacobian(i,:)
    end do
    100 format(10(f4.1,","))
    close(unit=10)
  end if

end program dense_jacobian
